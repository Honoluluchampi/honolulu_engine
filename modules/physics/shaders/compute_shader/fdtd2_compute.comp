#version 450

#extension GL_GOOGLE_include_directive : require

#include "../../common/fdtd2_config.h"
#include "../../common/fdtd_struct.h"

layout (local_size_x = fdtd2_local_size_x, local_size_y = fdtd2_local_size_y, local_size_z = 1) in;

// pressure
layout(std430, set = 0, binding = 0) buffer CurrentP { particle curr_p[]; };
layout(std430, set = 1, binding = 0) readonly buffer PreviousP { particle prev_p[]; };
layout(std430, set = 2, binding = 0) readonly buffer PreviousP2 { particle prev_p2[]; };
layout(std430, set = 3, binding = 0) writeonly buffer SoundBuf { float sound_buffer[]; };

// push constants
layout(push_constant) uniform Push { fdtd2_push push; };

// grid id
uint g_id(uint x, uint y) { return (x + 1) + (y + 1) * (push.x_grid + 1); }

// exciter constants
const float exc_grid_count = 5;
const float w = 0.012;
const float h = 0.0006;
const float k = 8000000;
const float rho = 1.1;
const float dx = 0.00383;
const float dp_max = k * h;
const float c_area = w * h;

void main() {
  uint i = gl_LocalInvocationID.x + gl_WorkGroupID.x * fdtd2_local_size_x;
  uint j = gl_LocalInvocationID.y + gl_WorkGroupID.y * fdtd2_local_size_y;

  if (i < push.x_grid && j < push.y_grid) // inside domain
  {
    uint idx = g_id(i, j);
    float state = prev_p[idx].values.w;
    if (state >= -1) {
      float pml = max(prev_p[idx].values.w, 0.f);

      // update pressure
      float right_is_domain = float(prev_p[g_id(i + 1, j)].values.w >= -1);
      float upper_is_domain = float(prev_p[g_id(i, j + 1)].values.w >= -1);

      float p = (prev_p[g_id(i, j)].values.z - push.p_fac *
      (prev_p[idx].values.x * right_is_domain - prev_p[g_id(i - 1, j)].values.x +
      prev_p[idx].values.y * upper_is_domain - prev_p[g_id(i, j - 1)].values.y)
      ) / (1 + pml);

      curr_p[idx].values.z = p;

      // re-calculate the current pressure to reduce the barrier synchronization
      if (i < push.x_grid - 1) {
        float right_is_domain_x1 = float(prev_p[g_id(i + 2, j)].values.w >= -1);
        float upper_is_domain_x1 = float(prev_p[g_id(i + 1, j + 1)].values.w >= -1);

        float p_x1 = (prev_p[g_id(i + 1, j)].values.z - push.p_fac *
        (prev_p[g_id(i + 1, j)].values.x * right_is_domain_x1 - prev_p[idx].values.x +
        prev_p[g_id(i + 1, j)].values.y * upper_is_domain_x1 - prev_p[g_id(i + 1, j - 1)].values.y)
        ) / (1 + pml);

        curr_p[idx].values.x =
        (prev_p[idx].values.x - push.v_fac * (p_x1 - p)) / (1 + pml);
      }

      if (j < push.y_grid - 1) {
        float right_is_domain_y1 = float(prev_p[g_id(i + 1, j + 1)].values.w >= -1);
        float upper_is_domain_y1 = float(prev_p[g_id(i, j + 2)].values.w >= -1);

        float p_y1 = (prev_p[g_id(i, j + 1)].values.z - push.p_fac *
        (prev_p[g_id(i, j + 1)].values.x * right_is_domain_y1 - prev_p[g_id(i - 1, j + 1)].values.x +
        prev_p[g_id(i, j + 1)].values.y * upper_is_domain_y1 - prev_p[idx].values.y)
        ) / (1 + pml);

        curr_p[idx].values.y =
        (prev_p[idx].values.y - push.v_fac * (p_y1 - p)) / (1 + pml);
      }

      if (idx == push.listener_index)
        sound_buffer[push.buffer_index] = p;
    }
    else if (state == -3) // exciter
    {
      float next_p = prev_p[g_id(i + 1, push.y_grid / 2)].values.z;
      float dp = push.input_pressure - next_p;
      float u_bore = c_area * (1 - dp / dp_max) * sqrt(2.f * dp / rho);
      if (dp <= 0.05 * dp_max)
        u_bore = 0.f;
      float shelf = 0.5 + 0.5 * tanh(4.f * (-1.f + (dp_max - dp) / (0.01 * dp_max)));
      curr_p[idx].values.x = u_bore * shelf / 0.000367;
    }
  }
}