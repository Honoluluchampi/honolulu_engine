#version 450

#extension GL_GOOGLE_include_directive : require

#include "../../common/fdtd2_config.h"
#include "../../common/fdtd_struct.h"

layout (local_size_x = fdtd2_local_size_x, local_size_y = fdtd2_local_size_y, local_size_z = 1) in;

// pressure
layout(std430, set = 0, binding = 0) buffer CurrentP { particle curr_p[]; };
layout(std430, set = 1, binding = 0) readonly buffer PreviousP { particle prev_p[]; };
layout(std430, set = 2, binding = 0) readonly buffer PreviousP2 { particle prev_p2[]; };
layout(std430, set = 3, binding = 0) writeonly buffer SoundBuf { float sound_buffer[]; };
layout(std430, set = 4, binding = 0) readonly buffer ActiveIdx { int is_active[]; };

// push constants
layout(push_constant) uniform Push { fdtd2_push push; };

// grid id
uint g_id(uint x, uint y) { return (x + 1) + (y + 1) * (push.x_grid + 1); }

// exciter constants
const float exc_grid_count = 5;
const float w = 0.012;
const float h = 0.0006;
const float k = 8000000;
const float rho = 1.1;
const float dx = 0.00383;
const float dp_max = k * h;
const float c_area = w * h;

// lossy wall filter constants
const float lwf_a0 = 0.f; //495.9;
const float lwf_a1 = 0.f; //-857.0;
const float lwf_a2 = 0.f; //362.8;
const float lwf_b0 = -1.35;
const float lwf_b1 = 0.40;

// u bore filter
const float ubf_a0 = 0.160863;
const float ubf_a1 = 0.321726;
const float ubf_a2 = 0.160863;
const float ubf_b1 = -0.590436;
const float ubf_b2 = 0.233886;

void main() {
  uint i = gl_LocalInvocationID.x + gl_WorkGroupID.x * fdtd2_local_size_x;
  uint j = gl_LocalInvocationID.y + gl_WorkGroupID.y * fdtd2_local_size_y;
  uint idx = g_id(i, j);

  if (i < push.x_grid && j < push.y_grid && is_active[idx] == 1.f) // inside domain
  {
    float state = prev_p[idx].values.w;
    if (state >= -1) {
      float pml = max(prev_p[idx].values.w, 0.f);

      // update pressure
      float p = (prev_p[g_id(i, j)].values.z - push.p_fac *
      (prev_p[idx].values.x - prev_p[g_id(i - 1, j)].values.x +
      prev_p[idx].values.y - prev_p[g_id(i, j - 1)].values.y)
      ) / (1 + pml);

      curr_p[idx].values.z = p;

      // re-calculate the current pressure to reduce the barrier synchronization
      // update x velo
      if (i < push.x_grid - 1) {
        float right_is_not_exc = float(prev_p[g_id(i + 1, j)].values.w != -3);

        float p_x1 = (prev_p[g_id(i + 1, j)].values.z - push.p_fac *
        (prev_p[g_id(i + 1, j)].values.x - prev_p[idx].values.x +
        prev_p[g_id(i + 1, j)].values.y - prev_p[g_id(i + 1, j - 1)].values.y)
        ) / (1 + pml);

        curr_p[idx].values.x =
        (prev_p[idx].values.x - push.v_fac * (p_x1 * right_is_not_exc - p)) / (1 + pml);

        if (prev_p[g_id(i - 1, j)].values.w == -2) {
          curr_p[g_id(i - 1, j)].values.x = (
          lwf_a0 * curr_p[idx].values.z
          + lwf_a1 * prev_p[idx].values.z
          + lwf_a2 * prev_p2[idx].values.z
          - lwf_b0 * prev_p[g_id(i - 1, j)].values.x
          - lwf_b1 * prev_p2[g_id(i - 1, j)].values.x
          );
        }
        if (prev_p[g_id(i + 1, j)].values.w == -2) {
          curr_p[idx].values.x = -(
          lwf_a0 * curr_p[idx].values.z
          + lwf_a1 * prev_p[idx].values.z
          + lwf_a2 * prev_p2[idx].values.z
          - lwf_b0 * prev_p[idx].values.x
          - lwf_b1 * prev_p2[idx].values.x
          );
        }
      }

      // update y velo
      if (j < push.y_grid - 1) {
        float p_y1 = (prev_p[g_id(i, j + 1)].values.z - push.p_fac *
        (prev_p[g_id(i, j + 1)].values.x - prev_p[g_id(i - 1, j + 1)].values.x +
        prev_p[g_id(i, j + 1)].values.y - prev_p[idx].values.y)
        ) / (1 + pml);

        curr_p[idx].values.y =
        (prev_p[idx].values.y - push.v_fac * (p_y1 - p)) / (1 + pml);

        if (prev_p[g_id(i, j - 1)].values.w == -2) {
          curr_p[g_id(i, j - 1)].values.y = (
            lwf_a0 * curr_p[idx].values.z
          + lwf_a1 * prev_p[idx].values.z
          + lwf_a2 * prev_p2[idx].values.z
          - lwf_b0 * prev_p[g_id(i, j - 1)].values.y
          - lwf_b1 * prev_p2[g_id(i, j - 1)].values.y
          );
        }
        if (prev_p[g_id(i, j + 1)].values.w == -2) {
          curr_p[idx].values.y = -(
            lwf_a0 * curr_p[idx].values.z
          + lwf_a1 * prev_p[idx].values.z
          + lwf_a2 * prev_p2[idx].values.z
          - lwf_b0 * prev_p[idx].values.y
          - lwf_b1 * prev_p2[idx].values.y
          );
        }
      }

      if (idx == push.listener_index)
      sound_buffer[push.buffer_index] = p;
    }

    else if (state == -3) // exciter
    {
      float next_p = prev_p[g_id(i + 1, push.y_grid / 2)].values.z;
      float dp = push.input_pressure - next_p;
      float u_bore = c_area * (1 - dp / dp_max) * sqrt(2.f * dp / rho);
      if (dp <= 0.05 * dp_max)
      u_bore = 0.f;
      float shelf = 0.5 + 0.5 * tanh(4.f * (-1.f + (dp_max - dp) / (0.01 * dp_max)));
      u_bore *= shelf;

      // butter-worth filter
      // input to the filter is contained by .values.y
      curr_p[idx].values.y = u_bore / 0.000367;
      curr_p[idx].values.x =
        ubf_a0 * curr_p[idx].values.y
      + ubf_a1 * prev_p[idx].values.y
      + ubf_a2 * prev_p2[idx].values.y
      - ubf_b1 * prev_p[idx].values.x
      - ubf_b2 * prev_p[idx].values.x;
    }
  }
}