#version 450

#extension GL_GOOGLE_include_directive : require

#include "../common.h"

layout (local_size_x = fdtd1_local_size_x, local_size_y = fdtd1_local_size_y, local_size_z = fdtd1_local_size_z) in;

// field
layout (std430, set = 0, binding = 0) buffer Curr { particle curr[]; };
layout (std430, set = 1, binding = 0) readonly buffer Prev  { particle prev[]; };
layout (std430, set = 2, binding = 0) readonly buffer Field { field_element field[]; };
layout (std430, set = 3, binding = 0) buffer Sound { float sound_buffer[]; };
layout (push_constant) uniform Push { fdtd_push push; };

#define M_PI 3.14159265359

const float PML_COUNT = 6.f;

float calc_vx(uint id)
{
  float p0 = prev[id - 1].p;
  float p1 = prev[id + 0].p;
  float p_inter = (p0 + p1) / 2.f;
  float p_term = p1 - p0;

  float s0 = M_PI * pow(field[id - 1].y_offset, 2.f);
  float s1 = M_PI * pow(field[id + 0].y_offset, 2.f);
  float s_inter = (s0 + s1) / 2.f;
  float s_term = p_inter / s_inter * (s1 - s0);
  float pml = clamp(float(int(id) - push.main_grid_count + 1) / PML_COUNT, 0.f, 1.f);

  return (prev[id].vx - push.v_fac * (p_term + s_term)) / (1.f + pml);
}

float calc_vy(uint id)
{
  float p_out = 0.f;
  float p_bore = prev[id].p;
  return prev[id].vy - push.v_fac * (p_out - p_bore);
}

float calc_p(uint id, float v1)
{
  float v0 = curr[id].vx;
  float v_inter = (v0 + v1) / 2.f;
  float v_term = v1 - v0;

  float s0 = M_PI * pow(field[id - 1].y_offset, 2.f);
  float s1 = M_PI * pow(field[id + 0].y_offset, 2.f);
  float s2 = M_PI * pow(field[id + 1].y_offset, 2.f);
  float s_inter0 = (s0 + s1) / 2.f;
  float s_inter1 = (s1 + s2) / 2.f;
  float s_term = v_inter / s1 * (s_inter1 - s_inter0);
  float pml = clamp(float(int(id) - push.main_grid_count + 1) / PML_COUNT, 0.f, 1.f);

  return (prev[id].p - push.p_fac * (v_term + s_term)) / (1.f + pml);
}

// exciter constants
const float w = 0.012;
const float h = 0.0006;
const float k = 8000000;
const float rho = 1.1;
const float dp_max = k * h;
const float c_area = w * h;

// u bore filter
const float ubf_a0 = 0.160863;
const float ubf_a1 = 0.321726;
const float ubf_a2 = 0.160863;
const float ubf_b1 = -0.590436;
const float ubf_b2 = 0.233886;

void main()
{
  uint id = gl_LocalInvocationID.x + gl_WorkGroupID.x * fdtd1_local_size_x;

  // update velocity
  if (1 <= id && id < push.whole_grid_count) {
    curr[id].vx = calc_vx(id);
  }

  // update pressure
  if (id < push.whole_grid_count) {
    // compute the next velocity not to add memory barrier
    float curr_v1 = calc_vx(id + 1);
    curr[id].p = calc_p(id, curr_v1);

    if (id >= push.main_grid_count) {
      curr[id].p = 0;
    }
  }

  // exciter
  if (id == 0) {
    float dp = push.mouth_pressure - prev[id].p;
    float u_bore = c_area * (1 - dp / dp_max) * sqrt(2.f * dp / rho);
    if (dp <= 0.05 * dp_max || push.mouth_pressure < 2000.f)
      u_bore = 0.f;
    float shelf = 0.5 + 0.5 * tanh(4.f * (-1.f + (dp_max - dp) / (0.01 * dp_max)));

    // butter-worth filter
    // input to the filter is contained by .values.y
    float a = 0.f;
    float curr_v = u_bore * shelf / 0.000367 * 4.f;
    curr[id].vx = a * prev[id].vx + (1.f - a) * curr_v;
  }

  // record sound buffer
  if (id == push.main_grid_count - 1) {
    sound_buffer[push.sound_buffer_id] = curr[id].p;
  }
}